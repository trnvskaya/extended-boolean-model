ASP/VBScript "Gotchas"
asp-classic vbscript decimal
I'm supporting/enhancing a web application written in Classic ASP/VBScript.  It has been about 10 years since I have used either in a day to day capacity.  I just ran across an issue that I would consider a "gotcha" and was wondering if others had similar things that I should learn to be aware of.
My issue:
I had to convert a Column in a SQL Table from float to decimal.  It turns out that decimal isn't a type that is really supported (or supported well) in vbscript. So the following code:
Dim var1, var2

var1 = rs("DecimalField1").Value
var2 = rs("DecimalField2").Value

If (var1 <> var2) Then
    'Do Something'
End If

Would blow up with a Type Mismatch error on the line:
If (var1 <> var2) Then

After much searching I found out that:
var1 = CDBL(rs("DecimalField1").Value)
var2 = CDBL(rs("DecimalField2").Value)

resolves the issue.  But that didn't seem like an obvious thing and it took me a while to figure out why the heck I was getting a Type Mismatch on that line.
So my question to everyone is, what other little quirks like this have you run across?  What are some things within ASP/vbscript that you would think of as "gotchas" that I should be on the lookout for?


Make sure that you use Set for object references:
 Dim rs : Set rs = CreateObject("ADODB.Recordset");

If you don't you'll either get an invalid variable reference or the default property
 Dim field      : Set field  = rs(0)
 Dim fieldValue : fieldValue = rs(0) 'Same as field.Value


You can leave out parenthesis when passing arguments to functions but only if the function call is the only expression in the statement:
 DoSomething withThisArgument
 Dim result : result = DoSomething(withThisArgument)
 result = DoSomething withThisArgument 'SYNTAX ERROR


Automatic variable allocation is probably one of the biggest gotchas.
Dim varA, varB

varA = varA + varV

Oops! What's varV you ask? Well.. I just mistyped B for V and everything still works perfectly.. that must be a good thing!
Except why isn't varB gets added to varA ?? That must be Microsoft's bug!

A common Gotcha when using HTML forms is a mismatch between the CharSet of the form page and the CodePage of the receiving page.
A typical example is where the Form page sets its CodePage to 65001 and the response CharSet to UTF-8.  This causes any values entered into the form to be posted back using UTF-8 encoding.  The receiving page leaves its CodePage set the System OEM code page such 1252. 
Counter intuatively ASP uses the Response.CodePage to determine how the characters in the form post should be interpreted, hence UTF-8 encoding get erroneously accepted as a set ot 1252 characters corrupting the input.
Sometimes this goes undetected because the page responds sets it Reponse.CharSet to UTF-8 but leaves its CodePage unchanged.  The result to the user appears good but data entered into the database is corrupt.
My recommendation is Save as UTF-8, use @codepage = 65001 in all pages and always set Response.CharSet to UTF-8.  This covers everything.

Repeat after me: All good VB programmers use Option Explicit
It will keep you from accidentally declaring a new variable and using it - thus throwing off whatever you are doing.
Beyond that, it depends on what you're doing.

Conditionals are a bit unintuitive at times.
For example, when dealing with Nulls: Although True and Null are not equal, the following expression will act like False. In this case it's good to check for Null explicitly using IsNull.
valueIsTrue = True
valueIsNull = Null
If valueIsTrue <> valueIsNull Then ...

Also, unlike some other languages, all parts of a condition are evaluated even if the first part is False. For example, the following example would return an error if myObject were Nothing:
If Not IsNothing(myObject) And myObject.IsValid() Then ...

The solution is to separate the conditions using nested Ifs or some other means:
If Not IsNothing(myObject) Then
    If myObject.IsValid() Then
        ...


Beware anytime you see the following line:
On Error Resume Next

That would be my caution in using classic ASP.

VBScript has a nastly way of letting you call subs with parenthesis if you only have 1 parameter. However, if that paramater is passed by reference, the return value does not come out if you use parenthesis:
<% OPTION EXPLICIT %>
<%

sub MakeLonger(byref something)
    something = "hello " & something 
end sub

dim msg
msg = "World"

MakeLonger(msg)
response.write msg
response.write "<br />"

MakeLonger msg
response.write msg

%>

Output is:
World 
hello World


classic ASP has a lot of gotchas if u never worked with it :)
i would recommend having a look at ajaxed library which is a still maintained classic asp library. It helps you to get rid of most common gotchas when dealing with legacy apps.
