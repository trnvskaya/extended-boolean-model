Mute Specific Application in Vista
windows-vista audio
I am in need of a way to mute a specific application in Vista.
Example: Mute just Firefox, but not all of the other application.  Much similar to muting a specific program from within the volume mixer in vista. 
If there is a program that will do this, i would appreciate that.  Otherwise if there is a way to do this, I will write a small app(Preferrably something .net).
EDIT: I want to automate this procedure, possibly key-map it.


I suggest using the built in Mixer in Vista...
Why do you want to use an 3rd party program?

Using AutoHotkey, this works even better than expected! Just a fast window flash and BOOM, done. 
Src: http://feebdack.com/knob/how_to_mute_a_single_application
#NoEnv ;// Recommended for new scripts
#Persistent ;// Recommended for new scripts
SendMode Input  ;// Recommended for new scripts
SetTitleMatchMode 2

;// Set VolumeMute to only silence Media Center
$f3::
    MuteMediaCenter()
    return

MuteMediaCenter()
{   
    ;// Open mixer
    Run sndvol 
    WinWait Volume Mixer
    ;// Mute Standard Media Center Process
    appName = Chrome
    MuteApp(appName)
    ;// Mute Netflix Media Center Process
    appName = Firefox
    MuteApp(appName)
    WinClose Volume Mixer
}

;// Volume Mixer must exist
MuteApp(appName) 
{
    ;// Find X position & width of textblock with text matching our appName
    ControlGetPos, refX, , refW, , % appName, Volume Mixer
    ;// Find button with left side within the width of the textblock
    x = -1
    while ( x != "") 
    {
        ;// A_Index is current loop iterationâused to find id
        tbIDX := (A_Index * 2) 
        ControlGetPos, x, , , , ToolbarWindow32%tbIDX%, Volume Mixer
        diff := x - refX
        if (diff > 0 && diff < refW)
        {
            ;// msgbox diff: %diff% refX: %refX% tbIDX: %tbIDX% x: %x% A_Index: %A_Index%
            ControlClick, ToolbarWindow32%tbIDX%, Volume Mixer
            break
        }
    }
}

