How can one open a PNG (image) file with VB6 into an RGB array, or R, G, B arrays
vb6 png
How can one open a PNG formatted image file with VB6?  Ideally, I (that is my customer) would like to have the PNG file open and placed into seperate R(ed), G(reen) and B(lue) arrays.
VB6 is not my tool of choice (for lack of knowledge) and I be thrilled if some one could point me in the right direction for a VB6 solution.


Thanks for the link, although not being a fluent VB guy (more C & ASM flavours), the code appears to be very BMP centric; not PNG.  
If that's the case, I have to believe you suggested the link because it would be a simple matter to make the code PNG'able, but I wouldn't know how to approach that.

You could try the open source FreeImage project. 

'1 form with :
'    1 picturebox : name=Picture1
'    1 commandbutton : name=Command1
Option Explicit

Private Type BITMAP
  bmType As Long
  bmWidth As Long
  bmHeight As Long
  bmWidthBytes As Long
  bmPlanes As Integer
  bmBitsPixel As Integer
  bmBits As Long
End Type

Private Declare Function GetObject Lib "gdi32" Alias "GetObjectA" (ByVal hObject As Long, ByVal nCount As Long, ByRef lpObject As Any) As Long
Private Declare Function GetBitmapBits Lib "gdi32" (ByVal hBitmap As Long, ByVal dwCount As Long, ByRef lpBits As Any) As Long
Private Declare Function SetBitmapBits Lib "gdi32" (ByVal hBitmap As Long, ByVal dwCount As Long, ByRef lpBits As Any) As Long

Private mbmpBits() As Byte
Private mudtBmp As BITMAP

Private Sub Command1_Click()
  ShowRed
'  ShowGreen
'  ShowBlue
End Sub

Private Sub Form_Load()
  Picture1.Picture = LoadPicture("c:\temp\pic.bmp")
End Sub

Private Sub Form_Unload(Cancel As Integer)
  Set Form1 = Nothing
End Sub

Private Sub ShowRed()
  Dim lngX As Long, lngY As Long
  ReadBits
  For lngX = 0 To mudtBmp.bmWidth - 1
    For lngY = 0 To mudtBmp.bmHeight - 1
      mbmpBits(0, lngX, lngY) = 0
      mbmpBits(1, lngX, lngY) = 0
    Next lngY
  Next lngX
  ShowBits
End Sub

Private Sub ShowGreen()
  Dim lngX As Long, lngY As Long
  ReadBits
  For lngX = 0 To mudtBmp.bmWidth - 1
    For lngY = 0 To mudtBmp.bmHeight - 1
      mbmpBits(0, lngX, lngY) = 0
      mbmpBits(2, lngX, lngY) = 0
    Next lngY
  Next lngX
  ShowBits
End Sub

Private Sub ShowBlue()
  Dim lngX As Long, lngY As Long
  ReadBits
  For lngX = 0 To mudtBmp.bmWidth - 1
    For lngY = 0 To mudtBmp.bmHeight - 1
      mbmpBits(1, lngX, lngY) = 0
      mbmpBits(2, lngX, lngY) = 0
    Next lngY
  Next lngX
  ShowBits
End Sub

Private Sub ReadBits()
  GetObject Picture1.Picture.Handle, Len(mudtBmp), mudtBmp
  With mudtBmp
    ReDim mbmpBits(0 To (.bmBitsPixel \ 8) - 1, 0 To .bmWidth - 1, 0 To .bmHeight - 1) As Byte
    GetBitmapBits Picture1.Picture.Handle, .bmWidthBytes * .bmHeight, mbmpBits(0, 0, 0)
  End With 'mudtBmp
End Sub

Private Sub ShowBits()
  SetBitmapBits Picture1.Picture.Handle, mudtBmp.bmWidthBytes * mudtBmp.bmHeight, mbmpBits(0, 0, 0)
  Erase mbmpBits
  Picture1.Refresh
End Sub

