How to raise an event from a SWF in a SWFLoader to a parent Flex application?
flex event-handling events swfloader
How can I raise an event from a SWF file loaded into a Flex application (using SWFLoader)?
I want to be able to detect
a) when a button is pressed
b) when the animation ends



You'll need to do 2 things:

Dispatch an event from the loaded swf. Make sure the event bubbles if you sent it from nested views. Bubbling can be set through the bubbles property of the event.
Listen to the event from your main application. I think you should be able to do that on the content property of the SWFLoader instance.
mySWFLoader.content.addEventListener("myEvent", myEventHandler);



As an adjunct to the answer by Christophe Herreman, and in case you were wondering, here is a way of making your own events...
package yourpackage.events
{
    import flash.events.Event;

    [Event(name="EV_Notify", type="yourpackage.events.EV_Notify")]
    public class EV_Notify extends Event
    {
    	public function EV_Notify(bubbles:Boolean=true, cancelable:Boolean=false)
    	{
    		super("EV_Notify", bubbles, cancelable);
    	}

    }
}

I have taken the liberty of setting the default value of bubbles to true and passing the custom event type to the super constructor by default, so you can then just say...
dispatchEvent(new EV_Notify());

In your particular case I doubt there are times when you would not want your event to bubble.
The prefix EV_ on the name is my own convention for events so I can easily find them in the code completion popups, you'll obviously pick your own name.
For the two cases you cite you can either have two events and listen for both of them, or add a property to the event which says what just happened, which is the approach which is taken by controls like Alert...
package yourpackage.events
{
    import flash.events.Event;

    [Event(name="EV_Notify", type="yourpackage.events.EV_Notify")]
    public class EV_Notify extends Event
    {
    	public static var BUTTON_PRESSED:int = 1;
    	public static var ANIMATION_ENDED:int = 2;

    	public var whatHappened:int;

    	public function EV_Notify(whatHappened:int, bubbles:Boolean=true, cancelable:Boolean=false)
    	{
    		this.whatHappened = whatHappened;
    		super("EV_Notify", bubbles, cancelable);
    	}

    }
}

then you call it as follows...
dispatchEvent(new EV_Notify(EV_NOTIFY.ANIMATION_ENDED));

you can then inspect the whatHappened field in your event handler.
private function handleNotify(ev:EV_Notify):void
{
    if (ev.whatHappened == EV_Notify.ANIMATION_ENDED)
    {
        // do something
    }
    else if (ev.whatHappened == EV_Notify.BUTTON_PRESSED)
    {
        // do something else
    }
    etc...
}

HTH

I took a lazier approach for raising the event inside flash
Flex:
<mx:SWFLoader  source="homeanimations/tired.swf"  id="swfTired" complete="swfTiredLoaded(event)" />

private function swfTiredLoaded(event:Event): void {
     mySWFLoader.content.addEventListener("continueClicked", continueClickedHandler);
}

Flash:
dispatchEvent(new Event("continueClicked", true, true));


I could not make this last approach work (with Flash CS4 and Flex 3).  I put the dispatchEvent call in one of the last frames of my Flash animation, but could not pick it up in Flex. 
I resorted to a counter variable and incrementing until I reached the known last frame number using the ENTER_FRAME event - which I can pick up using almost the same code.  
If I can pick this up, then why can't I pick up a custom event?

I believe its because you would be creating two seperate custom event class one in Flash and the other in Flex.
Dispatching one EV_NOTIFY.ANIMATION_ENDED from Flash may not be understood by Flex,since it has its own version of EV_NOTIFY.ANIMATION_ENDED.
